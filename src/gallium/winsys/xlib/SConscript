#######################################################################
# SConscript for xlib winsys

Import('*')

if env['platform'] == 'linux' \
        and 'mesa' in env['statetrackers'] \
        and ('softpipe' or 'i915simple' or 'trace') in env['drivers'] \
        and not env['dri']:

    env = env.Clone()

    env.Append(CPPPATH = [
        '#/src/mesa',
        '#/src/mesa/main',
    ])

    sources = [
        'glxapi.c',
        'fakeglx.c',
        'xfonts.c',
        'xm_api.c',
        'xm_winsys.c',
    ]

    drivers = [];
        
    if 'softpipe' in env['drivers']:
        drivers += [softpipe]

    if 'i965simple' in env['drivers']:
        drivers += [i965simple]
        sources += [
            'brw_aub.c',
            'xm_winsys_aub.c',
            ]
        
    if 'trace' in env['drivers']:
        env.Append(CPPDEFINES = 'GALLIUM_TRACE')
        drivers += [trace]

    # TODO: write a wrapper function http://www.scons.org/wiki/WrapperFunctions
    libgl = env.SharedLibrary(
        target ='GL',
        source = sources,
        LIBS = glapi + mesa + drivers + auxiliaries + env['LIBS'],
    )

    env.InstallSharedLibrary(libgl, version=(1, 5))
